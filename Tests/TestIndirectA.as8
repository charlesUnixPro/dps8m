"
" Test of address modifications to be run from TestIndirect .....
"
"
    option  Multics

    name    TAM     " This is the TAM_main segment
    
    entry  _main    " and we want to emtry TAM tests via TAM_main
    entry  _print

    segref	UtilsA, ioa 

    segdef  pi, E, fltTmp

        even
L1:     bss     1
L2:     bss     1

        even
LAQ:
LA:     bss     1
LQ:     bss     1

save2:      bss     ,2
save2p:     bss     ,2
save3p:     bss     ,2

        eight
rsave:  bss ,8

_main:

    eax0    _a
    eax1    u
    eax2    v
    eax3    w
    eax4    x
    eax5    y
    eax6    z

    tra     R_mods

"
" R mods ...
"
msgResult:  acc /Result: /

d9a:    desc9a  IBS,IBSsz
IBS:    aci     /If this works, that means were in TAM$.../
        equ     IBSsz, 42


R_mods:
    push

    tsp2	ioa
    acc     /R mods ...\r\n/

    tsp2    ioa
    acc     /'^S'\r\n/
    nop     d9a

    lda     u
    sta     L1
    tsp2    ioa
    acc     /Should be ... 006000 0100 03 is ^n\r\n/
    nop     L1

    ldq     0,x1
    stq     L1
    tsp2    ioa
    acc     /Should be ... 006000 0100 03 is ^n\r\n/
    nop     L1

    lda     2,x4
    sta     L1
    tsp2    ioa
    acc     /Should be ... 400000000003 is ^n\r\n/
    nop     L1

    ldq     y+10
    stq     L1
    tsp2    ioa
    acc     /Should be ... 000000000012 is ^n\r\n/
    nop     L1


    lda     t                   " 000005 000004 => a
    sta     L1
    sta     L2
    tsp2    ioa
    acc     /Should be ... 000005 000004 is ^w\r\n/
    nop     L1

    lda     L2      " restore A
    ldq     y, au   " 5 => q
    stq     L1
    tsp2    ioa
    acc     /Should be ... 5 is ^n\r\n/
    nop     L1


    lda     L2
    ldq     z, al   " 017 => q
    stq     L1
    tsp2    ioa
    acc     /Should be ... 17 is ^n\r\n/
    nop     L1


    ldq     t+1         
    stq     L1
    tsp2    ioa
    acc     /Should be ... 000006777776 (6,-2) is ^w\r\n/
    nop     L1

    ldq     L1
    lda     x,qu    " 3 => a
    staq    LAQ

    sta     L1
    tsp2    ioa
    acc     /Should be ... 3 is ^n\r\n/
    nop     L1

    ldaq    LAQ
    lda     y,ql    " y(-2) => a " 700

    sta     L1
    tsp2    ioa
    acc     /Should be ... 700 is ^n\r\n/
    nop     L1

_a: lda     0,ic   " 000000235004
    sta     L1
    tsp2    ioa
    acc     /Should be ... 000000235004 is ^w\r\n/
    nop     L1

    ldq  0123456,du   "0123456 => q -- none; operand has the form y || (00...0)18
    lda  0543210,dl   "0543210 => a --  none; operand has the form (00...0)18 || y
   
    staq    LAQ
    tsp2    ioa
    acc     "Should be ... 123456000000/000000543210 is ^n/^n\r\n"
    nop     LQ
    nop     LA

    tra     RI_mods
   

"
" RI mods ...
"

RI_mods:
    tsp2	ioa
    acc     /RI mods ...\r\n/


    lda     q,n*         " -2 => a
    ldq     q+1,*        " 042 => q
    staq    LAQ

    tsp2    ioa
    acc     "Should be ... 42/400000000002 is ^n/^n\r\n"
    nop     LQ
    nop     LA


    lda     2, dl       " 2 => AL
    ldx1    2, du        " 2 => x1
    ldq     q, 1*       " C(q+x1 + C(A)0,17) => 616 => q
    stq     L1
    tsp2    ioa
    acc     "Should be ... 616 is ^n\r\n"
    nop     L1


    lda     11, du      " 11 => AU
    ldx2    3, du        " 3 => x2
    ldq     q, 2*       " C(q+x2 + C(A)0,17) => 77 => q
    stq     L1
    tsp2    ioa
    acc     "Should be ... 77 is ^n\r\n"
    nop     L1


    ldq     4, ic*      " 02102 => q
    tra     here        " jump over stuff ...
    bss     2           " just waste 2 words
    arg     m, *

here:
    stq     L1
    tsp2    ioa
    acc     "Should be ... 2012 is ^n\r\n"
    nop     L1


    ldx0    2, du       " 2 => x0
    ldx1    2, du       " 2 => x1
    ldq     l, 0*       " l + 2 => du(m) => q

    stq     L1
    tsp2    ioa
    acc     "Should be ... ??? (since l+2 & m move) is ^n\r\n" 
    nop     L1


    eaa     1
    eax1    2
    ldq     k, au*      " x+2 => q (-3)

    stq     L1
    tsp2    ioa
    acc     "Should be ... 400000000003 is ^n\r\n"  " o-3
    nop     L1


    eaq     3
    lda     p,*         " y + 3 => 3

    sta     L1
    tsp2    ioa
    acc     /Should be ... 3 is ^n\r\n/
    nop     L1

    eax3    3
    eax5    5
    ldq     h,*     " x + 9 => 42
 
    stq     L1
    tsp2    ioa
    acc     "Should be ... 42 is ^n\r\n"  " o-3
    nop     L1

    tra     IR_mods

"
" IR mods ....
"
IR_mods:
    tsp2	ioa
    acc     /IR mods ...\r\n/

    lda     g, *n   " -1 => a
    sta     L1
    tsp2    ioa
    acc     "Should be ... 400000000001 is ^n\r\n"  " o-1
    nop     L1

    ldq    f, *dl   " &x => q
    stq     L1
    tsp2    ioa
    acc     "Should be ... ??? (this will move as x moves) is ^w\r\n"  "
    nop     L1

    eax4    13
    lda     c,  *1  " x+13 => -255 (400000000255) => a
    neg             " a = (377777777523)

    sta     L1
    tsp2    ioa
    acc     "Should be ... 377777777523 is ^n\r\n"  "
    nop     L1


    eax1    4
a:  lxl0    b,1*    " a + 5 => x0
    tra     here_2
b:  oct     111,222,333,444
    arg     bb, *ic
bb: arg     5, dl

here_2:

" DH02-01 Ex 1 pg 5-12
    ldq     1, dl
    lda     ir0, *QL    " C(b+1) (222) => a

    sta     L1
    tsp2    ioa
    acc     "Should be ... 000000000222 is ^n\r\n"  "
    nop     L1

    "call    ioa(msgResult, L1)  " 000000000222

" DH02-01 Ex 2 pg 5-12
    eax3    2
    eax5    3
    lda     ir1, *3     " C(b+2) (333) => a

    sta     L1
    tsp2    ioa
    acc     "Should be ... 000000000333 is ^n\r\n"  "
    nop     L1

    "call    ioa(msgResult, L1)  " 000000000333

" DH02-01 Ex 3 pg 5-12
    eax3    4
    eax5    5
    eaq     9
    eax7    7
    lda     ir2, *3     " C(x+9) (42) => a
    sta     L1

    tsp2    ioa
    acc     "Should be ... 00000000042 is ^n\r\n"  "
    nop     L1

    "call    ioa(msgResult, L1)  " 000000000042


" DH02-01 Ex 4 pg 5-12
    eax3    8
    ldq     9, dl
    lda     ir3, *dl    " C(A) = &w
    sta     L1

    tsp2    ioa
    acc     "Should be ... ??? is ^n (Yup, &w. But, &w will change)\r\n"  "
    nop     L1

    "call    ioa(msgResult, L1)  " Yup, &w. But, &w will change


" DH02-01 Ex 5 pg 5-12
    lda     10, dl
    lda     ir5, *al    " C(x + 10) (616) => a
    sta     L1
    
    tsp2    ioa
    acc     "Should be ... 616 is ^n\r\n"  "
    nop     L1

    "call    ioa(msgResult, L1)  " 000000000616

" DH02-01 Ex 6 pg 5-12
    eax3    11
    ldq     ir6, *n     " C(z) (13(8)) => q
    stq     L1
    
    tsp2    ioa
    acc     "Should be ... 00000000013 is ^n\r\n"  "
    nop     L1


" DH02-01 Ex 7 pg 5-12
    eax5    12
    lda     ir7, *n     " C(x + 12) (02012) => a
    sta     L1
    
    tsp2    ioa
    acc     "Should be ... 0000000002012 is ^n\r\n"  "
    nop     L1

" DH02-01 Ex 8 pg 5-12
    eax5    13
    lda     ir8, *      " C(x + 13) (-255 oct) => a
    neg                 " -a => a
    sta     L1
    
    tsp2    ioa
    acc     "Should be ... 34359738195 is ^d\r\n"  "
    nop     L1

" DH02-01 Ex 9 pg 5-12
    eax1    14
    ldq     ir9, *      " C(ir9c + 14) (7777777) => q
    stq     L1
    
    tsp2    ioa
    acc     "Should be ... 000007777777 is ^n\r\n"  "
    nop     L1

    tra IT_mods

"
" IT mods ...
"

IT_mods:
    tsp2	ioa
    acc     /IT mods ...\r\n/


" AL39 - Subtract delta (Td = 4) Example
        "Reference Computed Tally value 
        "count     address
        "1          c-d         t+1 
        "2          c-2d        t+2 
        "3          c-3d        t+3
        " ...
        "n          c-nd        t+n

IT_1:   ldq it1
        lda it1,sd
        
        sta L1
        
        tsp2    ioa
        acc     "Should be ... 000000000046 .. 000000000034 is ^n\r\n"  "
        nop     L1

        ttf IT_1
        
        tra IT_2

it1:    tallyd  it1b-1, -6, 2   "address, count, delta
it1a:   dec 0, 1, 2, 3, 4, 5, 6, 7, 8, 9,10
        dec   11,12,13,14,15,16,17,18,19,20
        dec   21,22,23,24,25,26,27,28,29,30
        dec   31,32,33,34,35,36,37,38,39,40
it1b:

" AL39 - Add delta (Td = 13) Example
        "Reference Computed Tally value 
        "count     address
        "1          c           t-1
        "2          c+d         t-2
        "3          c+2d        t-3
        "...
        "n          c+(n-1)d    t-n
        
IT_2:   ldq it2
        lda it2,ad
        
        sta L1
        tsp2    ioa
        acc     "Should be ... 000000000013 .. 000000000025 is ^n\r\n"  "
        nop     L1

        ttf IT_2

        tra IT_3

it2:    tallyd  it1a+11, 6, 2   "address, count, delta


" AL39 - Sequence character (Td = 12) Example

IT_3:   lda it3,sc

        sta L1
        tsp2    ioa
        acc     "Should be ... 'This ' is '^a'\r\n"  "
        nop     L1

        "call    ioa(msgResulta, L1)  " 'This '

        ttf IT_3

        tra IT_4
it3:    tallyb  it3b, 5, 0   "address, count, byte offset
it3b:   aci /This is a test of scr.../
it3c:
" try with 6-bit chars when bcc/bci? is ready .....

" AL39 - Sequence character reverse (Td = 5) Example
IT_4:   lda it4,scr

        sta L1
        tsp2    ioa
        acc     "Should be ... 'gfedc' is '^a'\r\n"  "
        nop     L1

        "call    ioa(msgResulta, L1)  "gfedc

        ttf IT_4
        
        tra IT_5

it4:    tallyb  it4c+1, -5, 3   " same as vfd 18/it4c+1,12/-5,1/1,5/3
        "tallyb  it3c-1, -5, 1   "address, count, byte offset
it4c:   aci     /abcdefgh/


" AL39 - Character indirect (Td = 10) Example

it5:    tallyb  it3b, , 3
it5b:   vfd 18/it5c+1,12/0,1/0,5/2
it5c:   bci "ABCDEFGHIJKLM"
it5d:   vfd 18/it5c,12/0,1/0,5/1
it5e:   vfd 18/it5c+2,12/0,1/0,5/0


IT_5:   lda it5,ci  " 163 => a

        sta L1
        "call    ioa(msgResulta, L1) " s

it5a:   lda it5b,ci
        sta L1
        tsp2    ioa
        acc     "Should be ... '000I' is '^g'\r\n"  "
        nop     L1

        "call    ioa(msgResultg, L1) " '00000I'

        lda it5d,ci
        sta L1
        "call    ioa(msgResultg, L1) " '00000B'
        tsp2    ioa
        acc     "Should be ... '000B' is '^g'\r\n"  "
        nop     L1

        lda it5e,ci
        sta L1
        "call    ioa(msgResultg, L1) " '00000M'
        tsp2    ioa
        acc     "Should be ... '000M' is '^g'\r\n"  "
        nop     L1

        tra IT_6


it6:    arg x+9
IT_6:   lda it6,i   " 042 => a

        sta L1
        tsp2    ioa
        acc     "Should be ... 42 is ^n\r\n"  "
        nop     L1

        "call    ioa(msgResult, L1) " 000000000042

        tra IT_7


" AL39 - Decrement address, increment tally (Td = 14)
IT_7:   ldq it7, di

        stq L1
        tsp2    ioa
        acc     "Should be ... 616,42,5,4,3,2,1 is ^n\r\n"  "
        nop     L1

        "call    ioa(msgResult, L1) " 616,42,5,4,3,2,1

        ttf IT_7

        tra IT_8
it7:    tallyd  x+11, -7

" AL39 - Increment address, decrement tally (Td = 16)
IT_8:   lda it8, id

        sta L1

        tsp2    ioa
        acc     "Should be ... 100, 200, ..., 700, 7777 is ^n\r\n"  "
        nop     L1

        "call    ioa(msgResult, L1) " 100, 200, ..., 700, 7777

        ttf IT_8

        tra IT_9
it8:    tallyd  x+15, 8

" DH02 - Increment address, decrement tally, and continue (Td = 17)
IT_9:   lda     it9, idc

        sta L1
        
        tsp2    ioa
        acc     "Should be ... o-1, o-2, o-3, 0, 1, 2, 3 is ^n\r\n"  "
        nop     L1


        "call    ioa(msgResult, L1) " o-1, o-2, o-3, 0, 1, 2, 3

        ttf     IT_9

        tra     IT_9B
it9:    tallyc  it9b, 7, 16 " * == n*
it9b:   arg     x + 0       " 1st ea
        arg     x + 1       " 2nd ea
        arg     x + 2       " 3rd ea
        arg     x + 3
        arg     x + 4
        arg     x + 5
        arg     x + 6

IT_9B:  lda     it9c, idc

        sta L1
        tsp2    ioa
        acc     "Should be ... 100 .. 700 is ^n\r\n"  "
        nop     L1

        "call    ioa(msgResult, L1) " 100 .. 700

        ttf     IT_9B

        tra     IT_10
it9c:   tallyc  it9d, 7, 41 "I
it9d:   arg     x + 15       " 1st ea
        arg     x + 16       " 2nd ea
        arg     x + 17       " 3rd ea
        arg     x + 18
        arg     x + 19
        arg     x + 20
        arg     x + 21


" DH02 pg 5-22 - Decrement address, increment tally, and continue (Td = 15)
IT_10:  lda     it10, dic   " x, y, z ...

        sta L1
        "call    ioa(msgResult, L1) " -1, 0, o13
        tsp2    ioa
        acc     "Should be ... -1, 0, o13 is ^n\r\n"  "
        nop     L1

        ttf     IT_10

        tra     IT_11
it10:   tallyc  it10b, -3, 41   "I
        arg     z
        arg     y
        arg     x
it10b:  null

" DH02 pg 5-22 - Decrement address, increment tally, and continue (Td = 15)
IT_11:  ldq     it11, dic       " aa1, aa2, c, aa4

        stq L1
        "call    ioa(msgResult, L1) " 012, 345, o-255, 670
        tsp2    ioa
        acc     "Should be ... 012, 345, o-255, 670 is ^n\r\n"  "
        nop     L1

        ttf     IT_11
        

        tra     IT_12
it11:   tallyc  adres3, -4, 48  "*n
        arg     ad4, *
        arg     c, *
        arg     ad2, *n
        arg     ad1, *n
adres3: bss     1
ad1:    arg     aa1
ad2:    arg     aa2
ad4:    arg     aa4

aa1:    oct     012
aa2:    oct     345
aa4:    oct     670

IT_12:

        " return to caller
        tsp2    ioa
        acc     /That's all folks!!! (TAM$_main)\r\n/
        
        return





"
" data .......
"
ir9:    arg ir9b, *1
ir9b:   arg ir9c, id
ir9c:   tally   _a,10      " doesn't really figure into the CA

ir8:    arg ir8b, *5
ir8b:   arg x, du

ir7:    arg ir7b, *5
ir7b:   arg x, du

ir6:    arg z,3 

ir5:    arg x, AD

ir3:    arg ir3b, 3*
ir3b:   oct 0,1,2,3,4,5,7777777,7 " was bss 8
        arg w, ql

ir2:    arg ir2b,*5
ir2b:   arg ir2c, *qu
ir2c:   arg x,7

ir1:    arg ir1b, 5*
ir1b:   bss 3
        arg b, IC
        
ir0:    arg b
c:      arg     d, n*
d:      arg     e, *4
e:      arg     x, ql

f:      arg     x, au

g:      arg     x,2

h:      arg     i,5*
i:      bss     5
        arg     j,3*
j:      bss     3
        zero    x+9 "m

k:      bss 1
        arg     x,1

l:      bss     2
        arg     l2,1*       " l2 + 2
l2:     bss     2
        arg     m, du

m:      arg     x+12
n:      arg     m, *

p:      arg     y,qu

q:      arg     x+1 " -2
        arg     x+9   " 042
        arg     x+8,al
        arg     x,au
r:      bss     1
s:      bss     1
t:      zero    5,4
        zero    6, -2
        zero    0123456, 0543210
u:      tally   3072,64,3   " should be 006000 0100 03
v:      TALLYB  2048,,41    "1 " should be 004000 0000 41
        TALLYC  w,10,41 "I
        TALLYD  512,4095,10 " should be 0 01000 7777 12
w:      bss	w2, 4
x:      oct -1,-2,-3,0,1,2,3,4,5,42,616,77, 02012, -255, 123, 100,200,300,400,500,600,700,7777
y:      dec 0,1,2,3,4,5,6,7,8,9,10
z:      dec 11,12,13,14,15,16,17,18,19,20
    
"
" some floating point stuff to play with ...
"
pi:     dec     3.1415926535897932
E:      dec     2.718281828
fltTmp: dec     -123.4567


_print: push


        tsp2    ioa
        acc     /\r\nThis is from TAM$_print ...\r\n/

        fld     pi
        emcall  6
        
        fld     E
        emcall  6

        fld     fltTmp
        emcall  6
        
        return
